load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@stardoc//stardoc:stardoc.bzl", "stardoc")
load(":toolchain.bzl", "ll_bootstrap_toolchain", "ll_toolchain")

LL_MODULES = [
    "actions",
    "args",
    "attributes",
    "bootstrap_library",
    "compilation_database",
    "defs",
    "deps",
    "driver",
    "environment",
    "init",
    "inputs",
    "internal_functions",
    "ll",
    "outputs",
    "providers",
    "toolchain",
    "tools",
]

bzl_library(
    name = "ll_bzl",
    srcs = [
        "{}.bzl".format(name)
        for name in LL_MODULES
    ],
    deps = [
        "@bazel_skylib//lib:dicts",
        "@bazel_skylib//lib:paths",
        "@bazel_tools//tools/build_defs/repo:http.bzl",
        "@bazel_tools//tools/build_defs/repo:utils.bzl",
    ],
)

[
    stardoc(
        name = "{}_docs".format(name),
        out = "{}.md".format(name),
        input = "{}.bzl".format(name),
        deps = [":ll_bzl"],
    )
    for name in LL_MODULES
]

filegroup(
    name = "docs",
    srcs = [
        ":{}_docs".format(name)
        for name in LL_MODULES
    ],
)

constraint_setting(name = "ll_exec")

constraint_value(
    name = "ll_exec_linux",
    constraint_setting = ":ll_exec",
)

platform(
    name = "ll_linux_exec_platform",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
        ":ll_exec_linux",
    ],
)

toolchain_type(name = "toolchain_type")

toolchain_type(name = "bootstrap_toolchain_type")

ll_bootstrap_toolchain(
    name = "ll_bootstrap_toolchain_x86-64-unknown-linux-gnu",
    exec_compatible_with = [":ll_exec_linux"],
    visibility = ["//visibility:public"],
)

toolchain(
    name = "ll_bootstrap_toolchain",
    exec_compatible_with = [":ll_exec_linux"],
    toolchain = "ll_bootstrap_toolchain_x86-64-unknown-linux-gnu",
    toolchain_type = ":bootstrap_toolchain_type",
    visibility = ["//visibility:public"],
)

ll_toolchain(
    name = "ll_toolchain_x86-64-unknown-linux-gnu",
    exec_compatible_with = [":ll_exec_linux"],
    visibility = ["//visibility:public"],
)

toolchain(
    name = "ll_toolchain",
    exec_compatible_with = [":ll_exec_linux"],
    toolchain = ":ll_toolchain_x86-64-unknown-linux-gnu",
    toolchain_type = ":toolchain_type",
    visibility = ["//visibility:public"],
)
